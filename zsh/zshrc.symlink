# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=/srv/git

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# tab completion from both ends
setopt completeinword

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

export PATH="$PATH:$HOME/.rvm/bin" # Add RVM to PATH for scripting

if [ -f ~/.rvm/scripts/rvm ]; then
    source ~/.rvm/scripts/rvm
fi

# Colorize maven output
# Thanks to https://github.com/builddoctor/maven-antsy-color

alias maven="command mvn"
function color_maven() {
    local BLUE="\x1b[0;34m"
    local RED="\x1b[0;31m"
    local GREEN="\x1b[0;32m"
    local YELLOW="\x1b[1;33m"
    local WHITE="\x1b[1;37m"
    local LIGHT_RED="\x1b[1;31m"
    local LIGHT_GREEN="\x1b[1;32m"
    local LIGHT_BLUE="\x1b[1;34m"
    local LIGHT_CYAN="\x1b[1;36m"
    local NO_COLOUR="\x1b[0m"
    maven $* | sed \
        -e "s/Tests run: \([^,]*\), Failures: \([^,]*\), Errors: \([^,]*\), Skipped: \([^,]*\)/${LIGHT_GREEN}Tests run: \1$NO_COLOUR, Failures: $RED\2$NO_COLOUR, Errors: $YELLOW\3$NO_COLOUR, Skipped: $LIGHT_BLUE\4$NO_COLOUR/g" \
        -e "s/\(\[\{0,1\}WARN\(ING\)\{0,1\}\]\{0,1\}.*\)/$YELLOW\1$NO_COLOUR/g" \
        -e "s/\(\[ERROR\].*\)/$RED\1$NO_COLOUR/g" \
        -e "s/\(\(BUILD \)\{0,1\}FAILURE.*\)/$RED\1$NO_COLOUR/g" \
        -e "s/\(\(BUILD \)\{0,1\}SUCCESS.*\)/$LIGHT_GREEN\1$NO_COLOUR/g" \
        -e "s/\(\[INFO\].*\)/$GREEN\1$NO_COLOUR/g"
        return $PIPESTATUS

}

#alias mvn=color_maven

# autocorrect is annoying the hell out of me, disable it now!
DISABLE_CORRECTION="true"
unsetopt correct
unsetopt correct_all
